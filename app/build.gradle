plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-android-extensions'
}
def version = rootProject.ext.android
def lib = rootProject.ext.dependencies

android {

    compileSdkVersion version.compileSdkVersion

    defaultConfig {
        applicationId "com.github2136.wardrobe"
        minSdkVersion version.minSdkVersion
        targetSdkVersion version.targetSdkVersion
        versionCode 1
        versionName "1.0.0"
        ndk {
            abiFilters "armeabi-v7a"
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    dataBinding {
        enabled = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix ".debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions { jvmTarget = JavaVersion.VERSION_1_8 }

    androidExtensions {
        experimental = true
    }
    android.applicationVariants.all { var ->
        var.outputs.all {
            outputFileName = "Wardrobe-${var.buildType.name}-${var.versionName}-${var.versionCode}-${releaseTime()}.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':basemvvm')

    implementation lib['kotlin-stdlib']
    implementation lib['android-appcompat']
    implementation lib["android-constraintlayout"]
    implementation lib["android-coordinatorlayout"]
    implementation lib["android-cardview"]
    //协程
    implementation lib["android-coroutines"]
    implementation lib["android-viewmodel-ktx"]
    implementation lib["android-lifecycle-ktx"]
    //workManager
    implementation lib["android-work"]
    implementation lib["android-work-ktx"]

    //sqlite数据库
    implementation lib["android-room"]
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt lib["android-room-compiler"]
    // optional - Kotlin Extensions and Coroutines support for Room
    implementation lib["android-room-ktx"]
    // optional - Test helpers
    testImplementation lib["android-room-test"]
    //图片查看选择器
    implementation lib["photoPicker"]
    implementation lib["photoView"]
    //图片加载
    implementation lib["glide"]
    kapt lib["glide-compiler"]

    androidTestImplementation lib["android-test"]
    androidTestImplementation lib["android-ext"]
    testImplementation lib["junit"]
    androidTestImplementation lib["espresso"]
}

static def releaseTime() {
    return new Date().format("yyMMdd", TimeZone.getTimeZone("GMT+08"))
}
